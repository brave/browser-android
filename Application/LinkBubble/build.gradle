buildscript {
    repositories {
        jcenter()
        flatDir { dirs '../../DexGuard/lib-6.1.10' }
        maven { url 'https://maven.fabric.io/public' }
    }
    dependencies {
        classpath ':dexguard:'
        classpath 'com.android.tools.build:gradle:1.1.3'
        classpath 'io.fabric.tools:gradle:1.19.0'
    }
}

// Manifest version information!
def versionMajor = 1
def versionMinor = 5
def versionPatch = 0
def versionBuild = 0 // bump for dogfood builds, public betas, etc.

apply plugin:'com.android.application'
if(buildMode == "dexguard") {
    apply plugin: 'dexguard'
    println "*** Apply plugin -> DEXGUARD"
}

apply plugin: 'io.fabric'

repositories {
    mavenCentral()
    maven { url 'https://maven.fabric.io/public' }
}

def gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
def buildTime = new Date().format("yyyy-MM-dd'T'HH:mm'Z'", TimeZone.getTimeZone("UTC"))

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.0"
    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 22

        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"

        buildConfigField "String", "GIT_SHA", "\"${gitSha}\""
        buildConfigField "String", "BUILD_TIME", "\"${buildTime}\""

    }

    productFlavors {
        playstore {
            applicationId='com.linkbubble.playstore'

            buildConfigField "String", "STORE_PACKAGE", "\"com.android.vending\""
            buildConfigField "String", "STORE_URL_PREFIX", "\"http://play.google.com/store/apps/details?id=\""
            buildConfigField "String", "STORE_FREE_URL", "\"http://play.google.com/store/apps/details?id=com.linkbubble.playstore\""
            buildConfigField "String", "STORE_PRO_URL", "\"http://play.google.com/store/apps/details?id=com.linkbubble.license.playstore\""
            buildConfigField "String", "STORE_MY_OTHER_APPS_URL", "\"https://play.google.com/store/apps/developer?id=Chris+Lacy\""
            buildConfigField "String", "PRO_PACKAGE_NAME", "\"com.linkbubble.license.playstore\""
            buildConfigField "String", "TAP_PATH_PACKAGE_NAME", "\"com.digitalashes.tappath.playstore\""
        }
    }

    signingConfigs {
        release {
            if(buildMode == "dexguard") {
                storeFile file(System.getenv("KEYSTORE_LOCATION"))
                keyAlias System.getenv("LINK_BUBBLE_KEY_ALIAS")
                storePassword System.getenv("KEYSTORE_PASSWORD")
                keyPassword System.getenv("LINK_BUBBLE_KEY_PASSWORD")
            }
        }
    }
    buildTypes {
        debug {
            applicationIdSuffix '.dev'
            versionNameSuffix '-dev'
        }

        release {
            if(buildMode == "dexguard") {
                proguardFile getDefaultDexGuardFile('dexguard-release.pro')
                proguardFile 'dexguard-project.txt'
            }
            proguardFile 'proguard-project.txt'
            signingConfig signingConfigs.release
        }
    }

    lintOptions {
        lintConfig file("lint.xml")
    }
}

dependencies {
    compile 'com.android.support:support-v4:22.2.0'
    compile "com.android.support:appcompat-v7:22.2.0"
    compile 'com.google.android.gms:play-services-base:7.5.0'
    compile 'se.emilsjolander:stickylistheaders:2.1.2'
    compile('com.crashlytics.sdk.android:crashlytics:2.2.4@aar') {
        transitive = true;
    }
    compile 'com.squareup.retrofit:retrofit:1.5.0'
    compile files('libs/otto-1.3.4.jar')
    compile files('libs/picasso-2.1.1.jar')
    compile files('libs/dexguard_util.jar')
    compile files('libs/jsoup-1.7.3.jar')
    compile files('libs/slf4j-android-1.5.8.jar')
    compile files('libs/palette.jar')
    //compile project(':Libraries:ViewPagerIndicator:library')
}

task showVersion << {
    println buildscript.configurations.classpath.resolvedConfiguration.firstLevelModuleDependencies.moduleVersion
}

crashlytics.verboseGradlePlugin = true

clean.dependsOn showVersion
