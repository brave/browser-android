buildscript {
    repositories {
        mavenCentral()
        flatDir { dirs '../../DexGuard/lib' }
        maven { url 'http://download.crashlytics.com/maven' }
    }
    dependencies {
        classpath ':dexguard:'
        classpath 'com.android.tools.build:gradle:0.8.0'
        classpath 'com.crashlytics.tools.gradle:crashlytics-gradle:1.+'
    }
}

apply plugin: 'android'
//apply plugin: 'dexguard'
apply plugin: 'crashlytics'

repositories {
    mavenCentral()
    maven { url 'http://download.crashlytics.com/maven' }
}

android {
    compileSdkVersion 19
    buildToolsVersion '19.0.0'
    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 19
        versionCode 1
        versionName "1.0 (dev)"
    }

    productFlavors {
        playstore {
            packageName='com.linkbubble.playstore'
            //buildConfigField "String", "PACKAGE_NAME", "com.linkbubble.playstore"
        }
    }

    signingConfigs {
        release {
            // (via http://stackoverflow.com/a/19350401/328679)
            storeFile file(System.getenv("KEYSTORE_LOCATION"))
            keyAlias System.getenv("ACTION_LAUNCHER_KEY_ALIAS")

            // These two lines make gradle believe that the signingConfigs
            // section is complete. Without them, tasks like installRelease
            // will not be available!
            storePassword "notYourRealPassword"
            keyPassword "notYourRealPassword"
        }
    }
    buildTypes {
        release {
            //proguardFile plugin.getDefaultDexGuardFile('dexguard-release.pro')
            //proguardFile 'dexguard-project.txt'
            proguardFile 'proguard-project.txt'
            signingConfig signingConfigs.release
        }
    }

    lintOptions {
        lintConfig file("lint.xml")
    }
}

dependencies {
    compile 'com.android.support:support-v4:19.0.+'
    compile 'se.emilsjolander:stickylistheaders:2.1.2'
    compile 'com.crashlytics.android:crashlytics:1.+'
    compile files('libs/otto-1.3.4.jar')
    compile 'com.squareup.picasso:picasso:2.+'
    compile files('libs/dexguard_util.jar')
    //compile project(':Libraries:ViewPagerIndicator:library')
}

task askForPasswords << {
    // Must create String because System.readPassword() returns char[]
    // (and assigning that below fails silently)
    def storePw = new String(System.console().readPassword("Keystore password: "))
    def keyPw  = new String(System.console().readPassword("Key password: "))

    android.signingConfigs.release.storePassword = storePw
    android.signingConfigs.release.keyPassword = keyPw
}

tasks.whenTaskAdded { theTask ->
    if (theTask.name.equals("packagePlaystoreRelease")) {
        theTask.dependsOn "askForPasswords"
    }
}

task showVersion << {
    println buildscript.configurations.classpath.resolvedConfiguration.firstLevelModuleDependencies.moduleVersion
}

clean.dependsOn showVersion
