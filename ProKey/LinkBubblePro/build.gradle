buildscript {
    repositories {
        mavenCentral()
        flatDir { dirs '../../../DexGuard/lib' }
    }
    dependencies {
        classpath ':dexguard:'
        classpath 'com.android.tools.build:gradle:0.12.1'
    }
}

// Manifest version information!
def versionMajor = 1
def versionMinor = 1
def versionPatch = 0
def versionBuild = 0 // bump for dogfood builds, public betas, etc.

if(buildMode == "dexguard") {
    apply plugin:'dexguard'
    println "*** Apply plugin -> DEXGUARD"
} else {
    apply plugin:'com.android.application'
    println "*** Apply plugin -> ANDROID"
}
repositories {
    mavenCentral()
}

def gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
def buildTime = new Date().format("yyyy-MM-dd'T'HH:mm'Z'", TimeZone.getTimeZone("UTC"))


android {
    compileSdkVersion 19
    buildToolsVersion '19.1.0'
    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 19

        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"

        buildConfigField "String", "GIT_SHA", "\"${gitSha}\""
        buildConfigField "String", "BUILD_TIME", "\"${buildTime}\""
    }

    productFlavors {
        playstore {
            applicationId='com.linkbubble.license.playstore'

            buildConfigField "String", "FREE_PACKAGE_NAME", "\"com.linkbubble.playstore\""
            buildConfigField "String", "STORE_FREE_URL", "\"http://play.google.com/store/apps/details?id=com.linkbubble.playstore\""
            buildConfigField "String", "STORE_PRO_URL", "\"http://play.google.com/store/apps/details?id=com.linkbubble.license.playstore\""
        }
    }

    signingConfigs {
        release {
            if(buildMode == "dexguard") {
                storeFile file(System.getenv("KEYSTORE_LOCATION"))
                keyAlias System.getenv("LINK_BUBBLE_KEY_ALIAS")
                storePassword System.getenv("KEYSTORE_PASSWORD")
                keyPassword System.getenv("LINK_BUBBLE_KEY_PASSWORD")
            }
        }
    }
    buildTypes {
        release {
            if(buildMode == "dexguard") {
                proguardFile getDefaultDexGuardFile('dexguard-release.pro')
                proguardFile 'dexguard-project.txt'
            }
            signingConfig signingConfigs.release
        }
    }

    lintOptions {
        lintConfig file("lint.xml")
    }
}

dependencies {
    compile files('libs/dexguard_util.jar')
}

